#!k
`1:"нгн/к, (c) 2019-2022 нгн, ГНУ АГПЛ, версия 3. въведи \\ за повече информация\n"
\d repl
`1:prompt:," " /задай 0x0720 за интеграция с редактора емакс
cmds:(,"a")!{`1:1:joinpath[dirname`argv 0]"LICENSE";}
dirname:{$[#x:"/"/-1_"/"\x;x;,"."]}
joinpath:{$[x~,".";y;"/"~*|x;x,y;x,"/",y]}
fmt:{$[x~(::);"";(`A~@x)&1<#x;"(",("\n "/`k'x),")\n";`k[x],"\n"]}
line0:{c:{0x07~*-2#*x}{(l;r):x;(1:1;r,,(-2_l))}/(x;());"\n"/(*|c),,*c}
line1:{$[#x;;:0];x:-1_x;$[(3>#x)&("\\"=*x)&~^(!cmds)?x 1;cmds[x 1]x 1;.[`1:fmt@.:;,x;{`0:`err[]}]];`1:prompt;1}
line:line1@line0@
{cmds::@[cmds;x[1]1;:;{y;`0:x}2_x]}'{(&x~\:80#"-")_x:(1+*&x~\:,"/")_-1_x}@0:`argv 1;
\d .
$["kw"~`argv 0;;{repl.line@1:`}::/`];

/
--------------------------------------------------------------------------------

\   помощ        \\         изход
\a  лиценз       \l файл.k  зареди
\0  типове       \d абв.где задай именно пространство; възстанови с  \d .
\+  глаголи      \t:n израз време(изтекли милисекунди след n пуска)
\:  вход-изход   \v         променливи
\'  наречия      \f         функции
\`  символи      \cd път    смени текущата директория
\h  обобщение    \друго     команда(чрез /bin/sh)
--------------------------------------------------------------------------------
\0
Типове:
списък атом
  `A         общ списък       ()   ,"ab"   (0;`1;"2";{3};%)
  `I    `i   цяло             0N -9223372036854775807 01b
  `F    `f   плаваща запетая  -0w -0.0 0.0 0w 1.2e308 0n
  `C    `c   знак             "a"   0x6263   "d\0\"\n\r\t"
  `S    `s   символ           `   `a   `"file.txt"   `b`cd`"ef"
  `M    `m   таблица, речник   +`a`b!(0 1;"23")   (0#`)!()
        `o   ламбда           {1+x*y#z}  {[a;b]+/*/2#,a,b}
        `p   проекция         1+   {z}[0;1]   @[;i;;]
        `q   композиция       *|:   {1+x*y}@
        `r   производен гл.   +/   2\   {y,x}':
        `u   едноглагол       +:   0::
        `v   двуглагол        +   0:
        `w   наречие          '   /:
        `x   външна функция
--------------------------------------------------------------------------------
\:
Входно-изходни глаголи
  0:x чети редове
x 0:y пиши редове
  1:x чети байтове
x 1:y пиши байтове
   <s отвори        fd:<`"файл.txt"
   >i затвори       >fd

x може да бъде файлов дескриптор (цяло) или символ или низ като:
 "файл.txt"
 "/път/до/файла"
 "host:port"
 ":port"         /host е 127.0.0.1 по подразбиране
--------------------------------------------------------------------------------
\+
Глаголи:    : + - * % ! & | < > = ~ , ^ # _ $ ? @ . 0: 1:
нотация:    [c]знак [i]цяло [n]число(цяло|плав.зап.) [s]символ [a]атом [d]речник
            [f]функция(1 арг.) [F]функция(2 арг.) [xyz]всякакво
специални:  var:y     присвои     a:1;a -> 1
            var::y    глобална    a:1;{a::2}[];a -> 2
            (v;..):y  разопаковай (b;(c;d)):(2 3;4 5);c -> 4
            :x        върни       {:x+1;2}[3] -> 4
            $[x;y;..] условие     $[0;`a;"\0";`b;`;`c;();`d;`e] -> `e
            o[..]     възврат     {$[x<2;x;+/o'x-1 2]}9 -> 34
            [..]      блок        [0;1;2;3] -> 3

::  себе си   ::12 -> 12
 :  дясно     1 :2 -> 2   "abc":'"d" -> "ddd"
 +x преобърни +("ab";"cd") -> ("ac";"bd")
N+N събери    1 2+3 -> 4 5
 -N отрицай   - 1 2 -> -1 -2
N-N извади    1-2 3 -> -1 -2
 *x пръв      *`a`b -> `a   *(0 1;"cd") -> 0 1
N*N умножи    1 2*3 4 -> 3 8
 %N кв.корен  %25 -> 5.0   %-1 -> 0n
N%N раздели   2 3%4 -> 0.5 0.75
 !i изброй    !3 -> 0 1 2   !-3 -> -3 -2 -1
 !I брояч     !2 3 -> (0 0 0 1 1 1;0 1 2 0 1 2)
 !d ключове   !`a`b!0 1 -> `a`b
 !S имена     a.b.c:1;a.b.d:2;!`a`b -> ``c`d
x!y речник    `a`b!1 2 -> `a`b!1 2
i!I ц.деление -10!1234 567 -> 123 56
i!I остатък   10!1234 567 -> 4 7
 &I къде      &3 -> 0 0 0   &1 0 1 4 2 -> 0 2 3 3 3 3 4 4
 &x дълб.къде &(0 1 0;1 0 0;1 1 1) -> (0 1 2 2 2;1 0 0 1 2)
N&N малко/и   2&-1 3 -> -1 2   0 0 1 1&0 1 0 1 -> 0 0 0 1
 |x обърни    |"abc" -> "cba"   |12 -> 12
N|N гол./или  2|-1 3 -> 2 3   0 0 1 1|0 1 0 1 -> 0 1 1 1
 <X нагоре    <"abacus" -> 0 2 1 3 5 4
 >X надолу    >"abacus" -> 4 5 3 1 0 2
 <s отвори    fd:<`"/path/to/file.txt"
 >i затвори   >fd
N<N по-малко  0 2<1 -> 1 0
N>N по-голямо 0 1>0 2 -> 0 0
 =X разпред.  ="abracadabra" -> "abrcd"!(0 3 5 7 10;1 8;2 9;,4;,6)
 =i ед.матр.  =3 -> (1 0 0;0 1 0;0 0 1)
N=N равно     0 1 2=0 1 3 -> 1 1 0
 ~x не        ~(0 2;``a;"a \0";::;{}) -> (1 0;1 0;0 0 1;1;0)
x~y съвпада   2 3~2 3 -> 1   "4"~4 -> 0   0~0.0 -> 0
 ,x единак    ,0 -> ,0   ,0 1 -> ,0 1   ,`a!1 -> +(,`a)!,,1
x,y слепи     0,1 2 -> 0 1 2  "a",1 -> ("a";1)
d,d вплети    (`a`b!0 1),`b`c!2 3 -> `a`b`c!0 2 3
 ^x дупки     ^(" a";0 1 0N;``a;0.0 0n) -> (1 0;0 0 1;1 0;0 1)
a^y запълни   1^0 0N 2 3 0N -> 0 1 2 3 1   "b"^" " -> "b"
X^y без       "abracadabra"^"bc" -> "araadara"
 #x дължина   #"abc" -> 3   #4 -> 1   #`a`b`c!0 1 0 -> 3
i#y преоразм. 5#"abc" -> "abcab"   -5#`a`b`c -> `b`c`a`b`c
I#y престрой  2 3#` -> (```;```)
f#y повтори   (3>#:')#(0;2 1 3;5 4) -> (0;5 4)   {2}#"ab" -> "aabb"
x#d вземи     `c`d`f#`a`b`c`d!1 2 3 4 -> `c`d`f!3 4 0N
 _n закръгли  _12.34 -12.34 -> 12 -13 /надолу
 _c мал.букви _"Ab" -> "ab"
i_Y пропусни  2_"abcde" -> "cde"   `a`c_`a`b`c!0 1 2 -> (,`b)!,1
I_Y нарежи    2 4 4_"abcde" -> ("cd";"";,"e")
f_Y оскуби    (3>#:')_(0;2 1 3;5 4) -> ,2 1 3
X_i премахни  "abcde"_2 -> "abde"
 $x низ       $(12;"ab";`cd;+) -> ("12";(,"a";,"b");"cd";,"+")
i$C плънка    5$"abc" -> "abc  "   -3$"a" -> "  a"
s$y към тип   `c$97 -> "a"   `i$-1.2 -> -1   `$"a" -> `a
s$y към цяло  `I$"-12" -> -12
 ?X различни  ?"abacus" -> "abcus"
 ?i случайно  ?2 -> 0.6438163747387873 0.8852656305774402
X?y намери    "abcde"?"bfe" -> 1 0N 4
i?x зар       3?1000 -> 11 398 293   1?0 -> ,-8164324247243690787
i?x раздай    -3?1000 -> 11 398 293 /guaranteed distinct
 @x тип       @1 -> `i   @"ab" -> `C   @() -> `A   @(@) -> `v
x@y приложи 1 {x+1}@2 -> 3   "abc"@1 -> "b"   (`a`b!0 1)@`b -> 1
 .S вземи     a:1;.`a -> 1   b.c:2;.`b`c -> 2
 .C оцени     ."1+2" -> 3
 .d стойности .`a`b!0 1 -> 0 1
x.y приложи N {x*y+1}. 2 3 -> 8   (`a`b`c;`d`e`f). 1 0 -> `d

@[x;y;f]   измени @["ABC";1;_:] -> "AbC"   @[2 3;1;{-x}] -> 2 -3
@[x;y;F;z] измени @["abc";1;:;"x"] -> "axc"   @[2 3;0;+;4] -> 6 3
.[x;y;f]   дълбай .[("AB";"CD");1 0;_:] -> ("AB";"cD")
.[x;y;F;z] дълбай .[("ab";"cd");1 0;:;"x"] -> ("ab";"xd")
.[f;y;f]   опитай .[+;1 2;"E:",] -> 3   .[+;1,`;"E:",] -> "E:'type\n"
?[x;y;z]   сплети ?["abcd";1 3;"xyz"] -> "axyzd"
--------------------------------------------------------------------------------
\`
Специални символи:
    `@x сериализирай      ` 7 8 9 -> 0x020300000000000000070809
    `?C десериализирай    `?0x020300000000000000070809 -> 7 8 9
   `j?C разбор на json    `j?"{\"a\":1,\"b\":[true,\"c\"]}" -> `a`b!(1.0;(1;,"c"))
   `k@x приготви за печат `k("ab";2 3) -> "(\"ab\";2 3)"
   `p@C разбор на k
 `hex@C шестнадесетично   `hex"ab" -> "6162"
   `x@x fork-exec         `x(("/bin/wc";"-l");"a\nbc\nd\n") -> "3\n"
   `t[] текущо време (микросекунди)
`argv[] списък от аргументи на командния ред
 `env[] речник от променливи на обкръжаващата среда
`prng[] `prng@I вземи/задай състоянието на генератора на случайни числа
                     s:`prng[];r:9?0;`prng s;r~9?0 -> 1
        `prng@0 ползвай текущото време да зададеш състояние
 `err@C хвърли грешка
Специални символи, достъпни само в интерпретатор създаден с "make k-libc":
 `sin@N тригонометрия   `sin 12.34 -> -0.22444212919135995
 `exp@N показателна ф-я `exp 1 -> 2.7182818284590455
  `ln@N логаритъм       `ln 2 -> 0.6931471805599453
--------------------------------------------------------------------------------
\'
Наречия:   ' / \ ': /: \:
   f' всеки1    #'("abc";3 4 5 6) -> 3 4
 x F' всеки2    2 3#'"ab" -> ("aa";"bbb")
   X' дв.търс.  1 3 5 7 9'8 9 0 -> 3 4 -1
   F/ прегъни   +/1 2 3 -> 6
   F\           +\1 2 3 -> 1 3 6
 x F/ с нач.ст. 10+/1 2 3 -> 16
 x F\           10+\1 2 3 -> 11 13 16
 i f/ i пъти    5(2*)/1 -> 32
 i f\           5(2*)\1 -> 1 2 4 8 16 32
 f f/ докато    (1<){$[2!x;1+3*x;-2!x]}/3 -> 1
 f f\           (1<){$[2!x;1+3*x;-2!x]}\3 -> 3 10 5 16 8 4 2 1
   f/ схождане  {1+1.0%x}/1 -> 1.618033988749895
   f\           (-2!)\100 -> 100 50 25 12 6 3 1 0
   C/ съедини   "ra"/("ab";"cadab";"") -> "abracadabra"
   C\ раздели   "ra"\"abracadabra" -> ("ab";"cadab";"")
   I/ от основа 24 60 60/1 2 3 -> 3723   2/1 1 0 1 -> 13
   I\ до основа 24 60 60\3723 -> 1 2 3   2\13 -> 1 1 0 1
  i': прозорец  3':"abcdef" -> ("abc";"bcd";"cde";"def")
i f': шаблон    3{x,"."}':"abcde" -> ("abc.";"bcd.";"cde.")
  F': вс.предш. -':12 13 11 17 14 -> 12 1 -2 6 -3
x F': с нач.ст. 10-':12 13 11 17 14 -> 2 1 -2 6 -3
x F/: вс.десен  1 2*/:3 4 -> (3 6;4 8)
x F\: вс.ляв    1 2*\:3 4 -> (3 4;6 8)
--------------------------------------------------------------------------------
\h
: ЗАДАЙ    ВЪРНИ     '  всеки|проз.|дв.търс.
+ събери   преобърни /  сгъни|съед.|от осн.|коментар
- извади   отрицай   \  scan |разд.|до осн.|следа
* умножи   пръв      ': всеки предшестващ
% раздели  кв.корен  /: всеки десен
! ост|рчнк избр|ключ \: всеки ляв
& малко|и  къде
| гол.|или обърни    /
< по-малко нагоре    многоредов коментар
> по-гол.  надолу    \
= равно    разпредели
~ съвпада  не        0: вх.-изх. редове
, слепи    единак    1: вх.-изх. байтове
^ без      дупки
# престрой дължина
_ пус|режи закръгли
$ към тип  низ       $[усл;да;не] УСЛОВИЕ
? търс|слч различни  ?[a;i;b]     вплети
@ прил(1)  тип       @[x;i;[f;]y] измени
. прил(n)  оцени     .[x;i;[f;]y] дълбай
граматика:  E:E;e|e e:nve|te| t:n|v v:tA|V n:t[E]|(E)|{E}|N
ограничения:  8 локални/арг, 64 константи, 256 байткод
\

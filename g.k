#!k
h:("// ngn/k, (c) 2019-2023 ngn, license: GNU AGPLv3 - https://codeberg.org/ngn/k/raw/branch/master/LICENSE"
   "// generated by g.k")

v:"xyzu" /variable names
t:"AGHILFC" /some types
T:t,"ESMmilfscopqruvwx" /all types
U:T,"UPRtTzZ",$`cC`lL`fF`sS`zZ`mM`mt`MT`zf`ZF`zc`ZC`zfc`ZFC`mMA`sSA`SA /types and pseudotypes

c:{+x@'!#'x} /cartesian product
e:,/{$[x~"@";*y;x~"$";y 1;x~"%";_*y;x]}\: /expand @:arg[0],$:arg[1],%:lower(arg[0])

r:()
r,:"@$ _$(@)"e/:c(v;"EKkmNnoRrtUVvWwXxyZz",t,(_t),"t",'U)
r,:"@$(a...) _$(@,a)"e/:c(v;"q128")
r,:"M@(a...) M_(@,a)"e/:v
r,:"@$(a...) P(%t$,a)"e/:c("XYZ";U)
r,:"@(a) ({A r_=(a);mr(@);r_;})"e/:v
r,:"_@(x) ((@*)_V(x))"e/:t
r,:"_%(x) _@(x)[i]"e/:t
r,:"_t@(x) (_t(x)==t@)"e/:"AEGHILFCSMmlfopqr"
r,:"_t@(x) (_t0(x)==t@)"e/:"iscuvwx"
r,:"N@(r) N(r,@(0))"e/:v
r,:"o@ _R(ce[t@])"e/:"AGLFCS"
r,:{"_",x,"(x) ((A*)_V(x))[",($x-"x"),"]"}'"xyz"
r,:{"_t",x,"(x) (_t",(-1_x),"(x)||_t",(-1#x),"(x))"}'(1<#:')#U
r,:{"R",($x),"(x,a...) case x:R",($-1+x),"(a)"}'2+!12
r,:"R@(a...) R(t@,a)"e/:"AEGHILFCSMmilfcsopqruvwx"

p!:p:`c$,/"aA"+\:!26 /pseudotype expansions
p["TUZtz"]:$`AEGHILFCS`opqruvw`EGHIL`ilfcsopqruvwx`il
R:" "\"cC fF GC ic iI ilf lL mM mMA MT MT_C MT_E opq opqr sS t T T_A T_AE T_E t_z U uvw z Z zc ZC ZC_E Z_E ZF ZF_E zZ"
r,:{X:?^/,/'p@"_"\x;"R",x,"(a...) R",($#X),"(",(","/"t",'X),",a)"}'R

"g.h"0:h,"#define ",/:r

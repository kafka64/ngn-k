#!k
r:("// ngn/k, (c) 2019-2021 ngn, license: GNU AGPLv3 - https://git.sr.ht/~ngn/k/blob/master/LICENSE"
   "// generated by g.k")
v:"xyzu";t:"ACBHILD";f:{r,:"#define ",/:(x[0]'). a@'!#'a:1_x;}
f({x,y," A",y,"(",x,")"};v;"knortv")    /properties
f({x,"N _N(",x,")"};v)                  /logical length
f({x,"R mR(",x,")"};v)                  /inc refcount
f({x,"(a) dex(",x,",a)"};v)             /free and return other
f({x,y," ((",y,"*)dat(",x,"))"};v;t)    /items
f({x,(_y)," ",x,y,"[i]"};v;t)           /i-th item
f({x,v[y]," ",x,"A[",($y),"]"};v;!3)    /nested
f({x,"t",y," (",x,"t==t",y,")"};v;"ACBHILDSMmcbhildsopqruvw") /of type?
f({x,"t",y," _",y,"(",x,"t)"};v;"FPQRtTzZ") /pseudotypes
f({x,"t",(_y),y," (",x,"t",(_y),"||",x,"t",y,")"};v;"CBHILDSMZ") /atom, list of type?
f({x,"tmt (",x,"tt||",x,"tm)"};v)       /atom|dict of type?
f({x,"tMT (",x,"tT||",x,"tM)"};v)       /list|tbl  of type?
f({x,"tmMA (",x,"tmM||",x,"tA)"};v)     /container?
f({x,y,"(a...) P(",(_x),"t",y,",a)"};"XY";"ACBHILDSMmcbhildsopqruvwTtZzFPQR"," "\"zZ cC bB iI hH lL dD sS mt MT mMA")
f'{x,y," ",x," ",y},/:":"\'" "\"C:bcdknotv BHIL:ijkmnv D:dv A:xyzu O:ACV"; /c type+var
f({"E",x,"(a...) E_(",x,",a)"};"cdilnoprstvz") /errors
f({"N",x,"(r) N1(",x,",r)"};v)          /error pass-through
f({"a",x,"0 mR(ce[t",x,"])"};"ACLDSm")  /empty
f({x,"q(a) _q(",x,",a)"};v)             /append
"g.h"0:r

P:{$[x<10;:(x>)#o 10;] /primes <x
 @[;0;:;2]1+2*&@[(-2!x)#0x010000010001010001010001000001;!3;:;0x01]{$[x@-2!y;@[x;-2!y*y+2*!-2!2+(-y)+(-y)!-1+2*#x;:;0x00];x]}/7+2*!0|-2!-5+_%x}
T:{p:2 3 5 7 11 13 17 19 23 29 31 37 41 43 47 53 59 61;$[x<62;:~^p?x;~&/p!'x;:0] /primality test
 q:x!*/2#,:;&/(1=ad)||/(x-1)=(s-1)q\ad:(x!*)/((-1+#d)q\2 3 61)@&d:(s:*&b)_b:|2\x-1}
tot:{(!x){[x;r;p]i:p*1+!(-p)!x-1;r[i]:(-p)!r[i]*p-1;r}[x]/P x} /totients
S1:{a:(x#1){[r;p]n:#r;m:1+(-p)!n-1;q:-1_(n>)(p*)\p;@[r;p*!m;*;@[m#p+1;(-1_q)*!'1+(--1_q)!'m-1;+;1_q]]}/p:P 2+_%x /sigma1: sum of divisors
 p:(#p)_P x;a[p*!'1+(-p)!'-1+#a]*:p+1;a[0]:0;a}
cf:{$[x=r*r:_%x;0;*|+(~(2*r)=*|:)({[x;r;m;d;a]m:(d*a)-m;d:(-d)!x-m*m;a:(-d)!r+m;m,d,a}[x;r].)\0 1,r]} /continued-fraction sqrt
gcd:*|(*:)(|!\)/,;M:-2!0N-1;S:{x@<x}
prm:{(x-1){,'/(!1+#x){(,(#*y)#x),y+y>x-1}\:x}/,,0};cmb:{$[x=1;,!y;(,&#'*'u),,'/u:i+(x-1)o'y-i:1+!1+y-x]};gon:{-2!y*4-x+y*2-x} /y-th x-agonal num
big:10\;sml:10/;car:{(~**x)_x:((*x),0)+0,*|x:0 10\x}/;add:{car@+/(&'(|/l)-l:#'x),'x:(x;y)};mul:{car@[&-1+(#x)+#y;(!#x)+\:!#y;+;x*\:y]}
max:{$[(#x)<#y;y;(#y)<#x;x;0<*(x-y)^0;x;y]};lt:{$[d:(#x)-#y;d<0;*>(x;y)]};sqrt:{(1,&l){{$[lt[mul[Y;Y:@[y;z;+;1]];x];Y;y]}[x;;z]/y}[x]/!l:-2!-1+#x}
O:0:`:o;E:{w:-."\\w";y:`k y[];,x,$[y~e:O x-1;`ok,w+."\\w";(`fail;y;e)]}

E[1]{+/&|/~5 3!\:!1000}
E[2]{+/(~2!)#*+-1_(*4000000>)(|+\)\!2}
E[3]{|/(~![;n]@)#P 2+_%n:600851475143}
E[4]{|/.'{x~|x}#$,/a*\:a:100_!1000}
E[5]{{x*(-gcd[x;y])!y}/1+!20}
E[6]{(b*b:+/a)-+/a*a:1+!100}
E[7]{P[200000]10000}
E[8]{|/*/(-|i)_'(i:!13)_\:.',/0:"i/p008.txt"}
E[9]{|/*/(ab,,c)@\:&(+/ab*ab)=c*c:1000-+/ab:!2#1000}
E[10]{+/P 2000000}
E[11]{|/,//*/''4''(a;+a;u,'a,'|u;(|u),'a,'u:&'!#a:.''" "\'0:"i/p011.txt")}
E[12]{-2!i*-1+i:*&500<@[;!2;:;1]@*/1++':'@[a;0;:;]0|-1+*a,:,~+/a:{[n;x]@[&n;q*!'(-q:-1_(n>)(x*)\x)!'n;+;1]}[n]'P 2+_%n:100000}
E[13]{sml 10#add/(0:"i/p013.txt")-"0"}
E[14]{(2=#:){$[#i:&y>1;(x i;(a*1+3*y)+(~a:2!y)*-2!y@:i);*x]}//2#,!1000000}
E[15]{*|n(+\)/(1+n:20)#1}
E[16]{+/1000{add[x;x]}/big 1}
E[17]{d:#"OneTwoThreeFourFiveSixSevenEightNine"
      +/(100*d;9*100*#"Hundred";9*99*#"And"
         10*+/(d;#"TenElevenTwelveThirteenFourteenFifteenSixteenSeventeenEighteenNineteen";10*#"TwentyThirtyFortyFiftySixtySeventyEightyNinety";8*d)
         #"OneThousand")}
E[18]{*{y+1_|':x}/|.''" "\'0:"i/p018.txt"}
E[19]{+/6=7!(+/m)+\0,-1_,/100#(3#,m),,@[;1;+;1]m:31 28 31 30 31 30 31 31 30 31 30 31}
E[20]{+/mul/big'1+!100}
E[21]{+/&(i=s s)>i=s:S1[n]-i:!n:10000}
E[22]{+/,/(1+!#a)*S a:(1-"A")+.'","\1:"i/p022_names.txt"}
E[23]{+/&@[n#1;a*n>a+:,\a:&S1[n]>2*!n:1+28123;:;0]}
E[24]{10/(!10){x((!10)_y),y}/(|1+!10)\999999}
E[25]{#(1000>#*:)(add\|:)\big'1 1}
E[26]{1+*>{(#a)-1+a?l:*|a:{l&2>+/x=l:*|x}{y,x!10**|y}[x]/,1}'1+!1000}
/E[27]@ **/1_{(x;a;b):x;i:&T'v:b+x*a+x;$[n<|/v;;~#i;(x;a;b);(x+1;a i;b i)]}/1,{x@'!#'x}(-1000+!2001;P 1000) /slow
E[28]{+/+\1,/4#'2*1+!-2!1001}
E[29]{#?,/1_n(i mul')\i:big'2+!n:-1+100}
E[30]{-1+/&a=!#a:+/(*/5#,!10)@!6#10}
E[31]{i:!201;*|(~i){[i;x;y]+/'x i-y*!'1+(-y)!i}[i]/1 2 5 10 20 50 100 200}
E[32]{+/?*|a@\:&a[2]=*/2#a:,'/10/''(0 2 5;0 1 5)_\:1+prm 9} /x*yyyy=zzzz or xx*yyy=zzzz
E[33]{f:{(-gcd/x)!x};*|f@*/{(</x)>=/x}#?,/{[f;x]a@&(&/'a>9)&(f x 1 2)~/:f'a:10/''x((0 1;0 2);(0 1;2 0);(1 0;0 2);(1 0;0 2))}[f]'+!3#10}
E[34]{+/3_&(!#*a)=(+/(*\1|!10)a)-(#a)-&1,9*\1,(-1+#a:!6#10)#10}
E[35]{2+#{x^x^{.,/|0 1_$x}'x}/{~|/^"1379"?$x}#P 1000000} /+2 because 2 and 5 don't match /[1379]+/
E[36]{+/{x~|x:2\x}#{x~|x:$x}#!1000000}
E[37]{+/(7<)#{&/T'.'|',\|$x}#,/{T#,/(!10)+/:10*x}\2 3 5 7}
E[38]{d:,/$1+!9;|/.'(d~S@)#(9=#:)#,/,'\$+\9#,!10000}
E[39]{p@:i:&(l+1)>p:2*/+\mn:1+|cmb[2;l:1000];p@:&1=gcd'/mn@\:i;*>#'=,/p*1+!'(-p)!'l}
E[40]{*/.'(,/$!*|a)a:6(10*)\1}
E[41]{p:P m:_%10000;|/,/({[m;p;x]$[~&/2 3 5 7!'x;0;x<m;~^p?x;&/p!'x]}[m;p]#10/1+prm@)'4 7}
E[42]{+/~^({-2!x*1+x}@!1+-2!*|/a)?a:+/'(1-"A")+1_'-1_'","\1:"i/p042_words.txt"}
E[43]{+/10/a@\:&~+/P[18]!'10/'3'1_a:prm 10}
E[44]{a@*&0<+/'&/0^@[&1+*|a;a;:;1](a+\:a;a+\:2*a:gon[5]1+!3000)}
E[45]{gon[3]@*2({x+a<|/a:3 5 6  gon'x}/1+)/1}
E[46]{*1_&@[(n+1)#!2;(n&(2*/2#,!1+_%-2!n)+\:p;p:P n:10000);:;0]}
E[47]{*1-k-&(k-1)&':/(k:4)=(&n){x[y*!(-y)!-1+#x]+:1;x}/p:P 2+_%n:1000000}
/E 48;h:_%m:10000000000;sqr:{m!b*(b:h!x)+2*h*(-h)!x};pow:{m!$[2!y;x;1]*$[y>1;m!sqr o[x;-2!y];1]}
/     m!+/{pow[x;x]}'1+!1000
E[49]{.,/$*^[;,1487 4817 8147]{=/1_-':x}#,/{x(+i)@&&/-1>':i:!3##x}'{2<#x}#p@.=S'$p:(999<)#P 10000}
E[50]{*|T#,/*|+{#*|x}{[p;n;x;y](1+x;y@&n>y+:0^p x+!#y)}[p;n]/\1,,p:P n:1000000} /slow
E[51]{&/{&/c@&x c:,/a@\:&7<+/x a:.[;0,,!2/#*a;:;0]a:(10/m*y)+/:10/'(~m:1_'-1_'!(#y:10\y)#2)*/:!10}[@[&n;p;:;1]]'p:P n:1000000} /slow
E[52]{{x+1<#?S'$2 3 4 5 6*x}/1}
E[53]{m:1000001;+/m=,/100(m&+':,[;0]@)\1}
E[54]{p:{100/(+/|\,/(s&f;14 23=d;f:1=#?x 1;s:~#((!5)+&/r)^r;113 122 1112=d:10/S@#'=r;1)),|S r+13*+/r=\:r:"23456789TJQKA"?*x:+x}
      +/(>/p'0 5_" "\)'0:"i/p054_poker.txt"}
E[55]{+/{50=*({(50>x)&~x&y~|y}/){(x+1;add[y;|y])}//(0;big x)}'!10000}
E[56]{|/+/',/mul'\99#,big'1+!99}
E[57]{+/>/'#''1000{(add[a;x 1];a:add/x)}\big'1 1}
E[58]{*({(10*y)>-2+2*x}.){(x+2;y++/T'(x*x)+1 2 3*x+1)}//7 8}
E[59]{f:8167 1492 2782 4253 12702 2228 2015 6094 6966 153 772 4025 2406 6749 7507 1929 95 5987 6327 9056 2758 978 2360 150 1974 74 /a-z freqs
      c:(8#2)\.'","\*0:"i/p059_cipher.txt"
      +/p@*>+/'@[&256;65 97+\:!26;:;2#,f]p:(2/~c=(#*c)#'(8#2)\)'+97+!3#26}
E[60]{h:(a@'1)@=*'a:.''{$[~T@.,/x;0;T@.,/|x]}#+p cmb[2;#p:$1_P 10000];&/+/'3{[h;x],/x,/:'{x^x^y}/'h x}[h]/a} /slow
E[61]{a:@[100#,!0;;,;]/'|0 100\'(4=#$:)#/:(3+!6)gon\:!150
      101*+/{[a;x;y]x{*y x}\|a 5,y}[a]/|*,/{[a;x](,x),/:&0|/'(!100)=(,/'@)/a 5,x}[a]'+prm 5}
E[62]{*/3#**(5=#:)#.=S'($*/3#,!10000)-"0"}
E[63]{+//a=!#a:#''30(i mul')\(#i:big'1+!9)#,big 1}
E[64]{+/~2!#'cf'!10000}
E[65]{+/*(big'!2){(add[x 1;mul[*x;y]];*x)}/|big'n#2,/+(1;2*1+!n:1+100;1)}
E[66]{pell:{$[x=r*r:_%x;:,0;]
            c:-1_*|+(~(2*r)=*|:)({[x;r;m;d;a]d:(-d)!x-m*m:(d*a)-m;m,d,(-d)!r+m}[x;r].)\0 1,r
            u:*|(big'!2){(x 1;add[*x;mul[y;x 1]])}/|big'c
            $[2!#c;add[big 1;add/2#,mul/2#,u];u]}
      *&a~\:max/a:pell'!1001}
E[67]{*{y+1_|':x}/|.'0:"i/p067_triangle.txt"}
E[68]{j:,/+i:(2*n)!0 1 3+\:2*!n:5;|/>[*/16#10]#(.,/$@[;j]@)'(1=#?+/@[;i]@)#+1+r@\:&&/(*r)</:(r:prm 2*n)2+2*!n-1}
E[69]{*>(-tot n)!'1000*!n:1000000}
E[70]{*{$[</x*|y;x;y]}/n,'a n:2_&=/+/'(9(10*)\1)(10\a:tot@*/m;!m:7#10)}
E[71]{*{$[>/x*|y;x;y]}/+(-7!-1+3*a;a:1+!1000000)}
E[72]{-1+/tot 1+1000000}
E[73]{t:~(n+1)#'~!'p:P n:12000;+/{[t;p;x]a:-3!x+2;b:1+-2!x;+/*/a_'b#'t@&~p!'x}[t;p]'4+!-3+n}
E[74]{+/60=1000000#{[a;x]x&61&1+x a}[a]/@[(#a:+/(|\0<a)*(*\1|!10)a:!3,6#10)#61;169 363601 1454 871 45361 872 45362;:;3 3 3 2 2 2 2]}
/E 75;c:&1+m:1500000
/     {x@:<x;l:+/x;$[l>m;:0;];c[l*!1+(-l)!m]+:1
/      o'+/((1 2 2;- 1 2 2;1 2 2);(- 2 1 2;2 1 2;2 1 2);(2 2 3;2 2 3;2 2 3))*x;}3 4 5
/     +/1=@[c;0;:;0]
E[76]{-1+*|*|100{(-1_x),,+\+/x*|=#x:(x,'*|+x),0}/,,1}
E[77]{*&5000<{{@[x;y+z;+;x z]}[;y]/(,x),!0|(#x)-y}/(,~!n),P n:100}
/https://en.wikipedia.org/wiki/Partition_function_(number_theory)#Recurrence_relations
/p(n) = sum (-1)^(k+1) * p(n-k(3k+1)/2),  -(sqrt(24n+1)-1)/6<=k<=(sqrt(24n+1)+1)/6, k!=0
E[78]{{1<#x}{$[0<a:1000000!+/(0|y x-gon[5]k)*(#k)#-1 1@2!2#k:e[0]+!1--/e:-6!(1-;1+)@'_%1+24*x;(x+1;y,a);x]}//2,,1 1}
E[79]{a:?.''0:"i/p079_keylog.txt";d:?,/a;e:?,/2''a;10/|{[d;e;x]?x,d@&~+/'d=\:*'(2=#:)#e^\:x}[d;e]/!0}
E[80]{+/{$[x=a*a:_%x;0;+/100#sqrt(10\x),&200]}'1+!100}
E[81]{*|*| {y&x+(M,'-1_'y)&M,-1_y                }[x]/+\+\'x:.'0:"i/p081_matrix.txt"}
E[82]{&/*|+{y&x+(1_y,M)&(M,-1_y)&M,'-1_'y        }[x]/  +\'x:.'0:"i/p082_matrix.txt"}
E[83]{*|*| {y&x+&/(M,-1_y;1_y,M;M,'-1_'y;1_'y,'M)}[x]/+\+\'x:.'0:"i/p083_matrix.txt"}
/E 84;n:200000;j:0 10 11 24 39 5;c:&40;p:0;m:0 /n:nr of moves to simulate, j:GO,JAIL,C1,E3,H2,R1, c:counters, p:position, m:consecutive doubles
/     f:{c[p]+:1;d:1+2?4;m+:1;m*:=/d;p::(#c)!p+/d
/        $[(p=30)|m>2;p::10                    /if on G2J or got 3 consecutive doubles, go to JAIL
/          ~^2 17 33?p;$[2>r:*1?16;p::j r;]    /if on CC1|CC2|CC3, possibly go to GO|JAIL
/          ~^i:7 22 36?p                       /if on CH1|CH2|CH3, go:
/          p::$[6>r:*1?16;j r                  / to the corresponding square from j
/               8>r;15 25 5 @i                 / to next R (railway company) - R1|R2|R3
/               8=r;12 28 12@i                 / to next U (utility company) - U1|U2|U1(again)
/               9=r;(#c)!p-3                   / back 3 squares
/               p]];}
/     n f/0;
/     100/3#>c
E[85]{*/2#,/&'a=&/&/a|:-a:2000000-t*\:t:-2!i*1+i:!2000}
/E 86;c:0;{c<1000000}{x{c+:$[~a=r*r:_%a:(y*y)+x*x;0;y>x;1+x--2!1+y;-2!y];}'3_!1+2*x+:1;x}/2
/E 87;+/@[&n;;:;1]f#,/a+/:f#,/b+\:f#{x*(#x)#p}b:f#{x*(#x)#p}a:(f:n>)#p*p:P 1+_%n:50000000
/E 88;a:,/{,/x,/:'(*'|'x)_'!'1+(-*/'x)!'2*L}\,'2_!2*L:12000 /all factorisations up to 2L
/     +/?2_@[0,L#0N-1;k*(L+1)>k:{(#x)+(*/x)-+/x}'a;&;*/'a]
/E 89;r:(,(!10)#'"M"),($`CDM`XLC`IVX)@\:(!0;0;0 0;0 0 0;0 1;1;1 0;1 0 0;1 0 0 0;0 2)
/     +/{(#x)-#,/r@'(4#10)\+/a*1-2*<':a:1 5 10 50 100 500 1000"IVXLCDM"?|x}'0:"i/p089_roman.txt"
E[90]{a:(+(f:{x+3*x=6})@10\i*i:1+!9);#&(&/|/(&/'|/''a=\:)'1|:\)'+c cmb[2;#c:+f cmb[6;10]]}
E[91]{(*/2#n)+/(a[1]=+/a 0 2)&/0<a:(+/*/2#,:)'a,,-/a:0 2_!4#1+n:50}
E[92]{+/(7(+/n#'(&'a*a:!10),\:)/~i)*{$[x<2;0;x=89;1;o@+/x*x:.'$x]}'i:!n:1000}
/E 93;R:{$[1<*|x;(-gcd/x)!x;x]} /rational
/     op:({(+/x*|y;*|x*y)};{(-/x*|y;*|x*y)};*;{x*|$[0>*y;-y;y]}) /+-*%
/     br:{(a;b;c;d):x;(f;g;h):y /all bracket placements over 4 values x and 3 functions y
/          fab:f[a;b];fbc:f[b;c];R'(h[g[fab;c];d];h[g[a;fbc];d];h[fab;g[c;d]];h[a;g[fbc;d]];h[a;g[b;f[c;d]]])}
/     hf:{+/&\@[~!100;{(99&0|*x)*x[1]=1}@+,/,/(x@+prm 4)br/:\:op@+!3##op;:;1]} /how far
/     10/*c@&v=|/v:hf'((!10),'1)c:+cmb[4;10]
/E 94 /sides: (a+d;a;a) where d=1 or d=-1.  p=(3a+d)/2, S^2=p(p-a+d)(p-a)^2, a=2b+1
/     /S^2=(3b+2)b(b+1)^2 or S^2=(3b+1)b^2(b+1);  (3b+1)^2-3m^2=1 or (3b+2)^2-3m^2=1, solve as pell equations
/     l:1+-2!*/9#10;s:0;{l>*x}{s+:P*l>P:(3+6*-3!*x)+0N -1 1@3!*x:(+/2 3*x;+/1 2*x);x}/2 1;s
E[95]{&/,/c@&l=|/l:#'c:(a@)\'?*(~~/)(b@')/b:@\2#,a:S1[n]-!n:1000000}
/E 96;p,:,3/-3!p:!9 9
/     +/{10/3#*(,x)(,/{@[x;y;:;]'(1+!9)^x@&|/p=p@'y}')/&~x:.',/1_"\n"\x}'1_"G"\1:"i/p096_sudoku.txt"
/E 97;mod:{(- 10&#x)#x};sqr:{mod mul[x;x]};prd:{mod mul[x;y]}/;pow:{prd@((-1+#e)sqr\x)@&e:|2\y}
/     sml mod add[big 1;mul[big 28433;pow[big 2;7830457]]] /28433*(2^7830457)+1 mod 10^10
/E 98;p:{,/{x@+c,'|c:cmb[2;#x]}'{1<#x}#x@.=S'x}
/     m:|/#'*'a:p 1_'-1_'","\1:"i/p098_words.txt"
/     f:{m/,/(*x)?/:x}
/     |/.',/b@&~^(?f'a)?f'b:p@${x*x}@!1+_%*/m#10
/E 99;d:10 /precision
/     fp:{x:big x;(#x;d#x,&d)}
/     fmul:{r:mul[x 1;y 1];$[4<r d;r:car@@[r;d-1;+;1];];(x[0]+y[0]+(#r)-2*d;d#r)}
/     fpow:{fmul/((-1+#e){fmul[x;x]}\fp x)@&e:|2\y}
/     1+*&r~\:r@*>r:fpow/'.'0:"i/p099_base_exp.txt"
E[100]{-2!1+*|(1000000000001>-2!1+*:)(2(+/(1 1;2 1)*)/)/1 1} /k(k-1)/n(n-1)=1/2, x=2n-1, y=2k-1, x^2-2v^2=-1
/                                                             start with x=y=1; while n<=10^12, produce a new solution twice

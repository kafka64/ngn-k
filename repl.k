#!k
` 0:"ngn/k, (c) 2019-2021 ngn, GNU AGPLv3 - https://git.sr.ht/~ngn/k/blob/master/LICENSE";
repl.h:1_(""
 " :x RETURN    {:x+1;2}[3] -> 4"
 "x:y SET       a:1; a -> 1   (b;(c;d)):(2 3;4 5); c -> 4"
 "x:y right     f:(:);f[1;2] -> 2"
 "x+y add       1 2+3 -> 4 5"
 " +x transpose +(\"ab\";\"cd\") -> (\"ac\";\"bd\")"
 "x-y subtract  1-2 3 -> -1 -2"
 " -x negate    - 1 2 -> -1 -2"
 "x*y multiply  1 2*3 4 -> 3 8"
 " *x first     *(0 1;\"cd\") -> 0 1"
 "x%y divide    4 3%2 -> 2 1   4 3%2.0 -> 2.0 1.5"
 " %x sqrt      %25 -> 5.0   %-1 -> 0n"
 "x!y mod       10!1234 567 -> 4 7"
 "x!y div       -10!1234 567 -> 123 56"
 "x!y dict      `a`b!1 2 -> `a`b!1 2"
 " !x enum      !3 -> 0 1 2   !-3 -> -3 -2 -1"
 " !x odometer  !2 3 -> (0 0 0 1 1 1;0 1 2 0 1 2)"
 "x&y min       2&-1 3 -> -1 2"
 "x&y and       0 0 1 1&0 1 0 1 -> 0 0 0 1"
 " &x where     &1 0 1 4 2 -> 0 2 3 3 3 3 4 4"
 " &x deepwhere &(0 1 0;1 0 0;1 1 1) -> (0 1 2 2 2;1 0 0 1 2)"
 "x|y max       2|-1 3 -> 2 3"
 "x|y or        0 0 1 1|0 1 0 1 -> 0 1 1 1"
 " |x reverse   |\"abc\" -> \"cba\"   |12 -> 12"
 "x<y less      0 2<1 -> 1 0"
 " <x asc       <\"abacus\" -> 0 2 1 3 5 4"
 " <x open      fd:<`:/path/to/file.txt"
 "x>y more      0 1>0 2 -> 0 0"
 "x=y equal     0 1 2=0 1 3 -> 1 1 0"
 " =x group     =\"abracadabra\" -> \"abrcd\"!(0 3 5 7 10;1 8;2 9;,4;,6)"
 " =x unitmat   =3 -> (1 0 0;0 1 0;0 0 1)"
 "x~y match     2 3~2 3 -> 1   \"4\"~4 -> 0   0~0.0 -> 0"
 " ~x not       ~(0 2;``a;\"a \\0\";::;{}) -> (1 0;1 0;0 0 1;1;0)"
 "x,y concat    0,1 2 -> 0 1 2  \"a\",1 -> (\"a\";1)"
 " ,x enlist    ,0 -> ,0   ,0 1 -> ,0 1   ,`a!1 -> +(,`a)!,,1"
 "x^y fill      1^0 0N 2 3 0N -> 0 1 2 3 1   \"b\"^\" \" -> \"b\""
 "x^y without   \"abracadabra\"^\"bc\" -> \"araadara\""
 " ^x null      ^(\" a\";0 1 0N;``a;0.0 0n) -> (1 0;0 0 1;1 0;0 1)"
 "x#y reshape   3#2 -> 2 2 2   2 3#` -> (```;```)"
 "x#y filter    {x>1}#0 2 1 3 5 4 -> 2 3 5 4"
 " #x length    #\"abc\" -> 3   #4 -> 1   #`a`b`c!0 1 0 -> 3"
 "x_y drop      2_\"abcde\" -> \"cde\"   `b_`a`b`c!0 1 2 -> `a`c!0 2"
 "x_y remove    \"abcde\"_2 -> \"abde\""
 " _x floor     _12.34 -12.34 -> 12 -13"
 " _x lowercase _\"Ab\" -> \"ab\""
 "x$y cast      `c$97 -> \"a\"   `d$1 -> 1.0   `$\"a\" -> `a"
 " $x string    $(12;\"ab\";`cd;+) -> (\"12\";(,\"a\";,\"b\");\"cd\";,\"+\")"
 "x?y find      \"abcde\"?\"bfe\" -> 1 0N 4"
 "x?y random    3?1000 -> 11 398 293   1?0 -> ,-8164324247243690787"
 "x?y deal      -3?1000 -> 11 398 293 /guaranteed distinct"
 " ?x uniq      ?\"abacus\" -> \"abcus\""
 "x@y apply(1)  {x+1}@2 -> 3   \"abc\"@1 -> \"b\"   (`a`b!0 1)@`b -> 1"
 " @x type      @1 -> `b   @\"ab\" -> `C   @() -> `A   @(@) -> `v"
 "x.y apply(n)  {x*y+1}. 2 3 -> 8   (`a`b`c;`d`e`f). 1 0 -> `d"
 " .x eval      .\"1+2\" -> 3   .`a`b!0 1 -> 0 1"
 ""
 "@[x;y;z]   amend @[\"ABC\";1;_:] -> \"AbC\"   @[2 3;1;{-x}] -> 2 -3"
 "@[x;y;z;u] amend @[\"abc\";1;:;\"d\"] -> \"adc\"   @[2 3;0;+;4] -> 6 3"
 ".[x;y;z;u] drill .[(\"ab\";\"cd\");1 0;:;\"x\"] -> (\"ab\";\"xd\")"
 ".[x;y;z]   try   .[+;1 2;{x}] -> 3   .[+;1,`;{x}] -> \"typ\""
 ""
 "$[x;y;z] COND /if x then y else z"
 ""
 "f'x  each      #'(\"abc\";3 4 5 6) -> 3 4"
 "f'x  slide     3'\"abcdef\" -> (\"abc\";\"bcd\";\"cde\";\"def\")"
 "f'x  bin       1 3 5 7 9':8 9 0 -> 3 4 -1"
 "f/x  fold      +/1 2 3 -> 6"
 "f\\x  scan      +\\1 2 3 -> 1 3 6"
 "f/x  join      \"ra\"/(\"ab\";\"cadab\";\"\") -> \"abracadabra\""
 "f\\x  split     \"ra\"\\\"abracadabra\" -> (\"ab\";\"cadab\";\"\")"
 "f/x  decode    24 60 60/1 2 3 -> 3723   2/1 1 0 1 -> 13"
 "f\\x  encode    24 60 60\\3723 -> 1 2 3   2\\13 -> 1 1 0 1"
 "f':x eachprior -':12 13 11 17 14 -> 12 1 -2 6 -3"
 "f/:x eachright 1 2*/:3 4 -> (3 6;4 8)"
 "f\\:x eachleft  1 2*\\:3 4 -> (3 4;6 8)"
 ""
 "  0:x read  lines"
 "x 0:y write lines"
 "  1:x read  bytes"
 "x 1:y write bytes"
 ""
 "   `@x serialize     ` 7 8 9 -> 0x020300000000000000070809"
 "   `?x deserialize   `?0x020300000000000000070809 -> 7 8 9"
 "  `k@x pretty-print  `k 7 8 9 -> \"7 8 9\""
 "  `p@x parse"
 " `cf@x const-fold"
 "  `t[] time(millis)"
 "  `x@x fork          `x((\"/bin/wc\";\"-l\");\"a\\nbc\\nd\\n\") -> \"3\\n\""
 "`hex@x hex           `hex\"ab\" -> \"6162\""
 "  `j?x parse json    `j?\"{\\\"a\\\":1,\\\"b\\\":[true,\\\"c\\\"]}\" -> `a`b!(1.0;(1;,\"c\"))"
 "`sin@x trigonometry  `sin 12.34 -> 0.22444212919135995"
 "`md5@x `sha1@x `sha256@x `sha3@x `aes  crypto"
 "  `e[] print error"
 ""
 "/comment"
 "\\command"
 "\\\\ exit"
 "\\t:n expr  time"
 "\\l file.k  load"
 "\\w         workspace"
 "expr0 \\expr1  trace")
repl.fmt:{$[x~(::);"";(`A~@x)&1<#x;"(",("\n "/`k'x),")\n";`k[x],"\n"]}
repl.rep:{` 1:," ";x:1:`;$[~#x;:0;];x:-1_x;$[(x~,"\\")|x~"\\h";:#` 0:repl.h;];.[{` 1:repl.fmt[. x]};,x;`e@];1}
{x}repl.rep/1;
\\

abs:{x|-x};sgn:{(x>0)-x<0};gcd:*|(*:)(|!\)/,;lcm:{(-gcd[x;y])!x*y};prm:{(x-1){,'/(!1+#x){(,(#*y)#x),y+y>x-1}\:x}/,,0}
D:1-3\32 16;I:{T/(0;x;0;y;!0)};E:{99=100!@/x 1 0}
T:{[i;a;r;u;v]p:(a;::;r+a@)[|3#(5#10)\a i]@'i+1+!3;c:100!a i
 $[c=3;(a[*p];u):(*u;1_u);c=4;v,:a@*p;|/c=5 6;i::(i+3;a p 1)2!c+~a@*p;~^j:1 2 7 8?c;a[p 2]:(+;*;<;=)[j]/a 2#p;c=9;r+:a@*p]
 (i+0|0 4 4 2 2 0 0 4 4 2 c;a;r;u;v)}.

a:.'0:"01";(+/-2+-3!a;+//1_(0|-2+-3!)\a)
a:.'","\*0:"02";f:{I[@[a;1 2;x];!0].1 0};(f 12 2;*&19690720=f'+!2#100)
a:0:"03";d:+"RDLU"!D;p:+\''b:{0,'(d@\:*'x)*\:.'1_'x:","\x}'a;g:|\:/'&':''+:;c:&,/&/~g[p]>r:-g@-p;f:{&/x[c]^0}
 (f@,/+/abs r;f@,/(+\:/+\'abs@+/'b)-+/+/abs(r-*p;r-\:'*|p))
a:{x+!1+y-x}/.'"-"\*0:"04";(+/{(|/=':x)>|/<':x:$x}'a;+/{(|/2=#'.=x)>|/<':x:$x}'a)
a:.'","\*0:"05";{*|*|I[a;,x]}'1 5
a:|+")"\'0:"06";(+/{x&1+0^x b}/0W-b=b:?/a;#2_,/^/'|:\(!/a)\','$`SAN`YOU)
a:.'","\*0:"07";p:+prm 5
 (|/*+0{*|I[a]y,x}/'p;|/{*|*|*|*|({~E z x}.)({z[x;3],:y;z[x;4]:!0;z[x]:{~(#x 4)|E x}T/z x;(5!x+1;*|*|z x;z)}.)/0 0,,{(0;a;0;x;!0)}'x}'p+5)
a:(0N;*/n:6 25)#*0:"08";0N!*/+/'"12"=\:a@*<+/'"0"=a;" #"n#{y^'x}/"01"?a
a:.'","\*0:"09";{**|I[a,&200;,x]}'1 2
a:"#"=0:$10;K:1+2*k:-1+|/n:(#a;#*a);t:@[;K/2#k;-1]@,/(|'|1_'t+2*m),'t:(|+t),1_t+m:|//t:s#(?t@<t)?t:+/>/t*\:'t:!s:2#1+k
 c:#'?'(2##*p)#t@K/k+,/'p-/:'p:n\&,/a;(-1+|/c;100/|p@'({x^0N}@,/+h@\:!l:|/#'h:{y@<x y}[+/d*d]'h@H@<H:!h:=t@K/k+d:p-p@'*>c)200)
a:(.'","\*0:$11),&300;b:*/31#2
 g:{[p;d;m;h]m[3]:,0^h@b/p;m:{$[1<#*|x;0;~E x]}T/m;$[E m;:h;];v:m 4;m[4]:!0;h[b/p]:*v;p+:D d:4!d+1-2*v 1;(p;d;m;h)}.
 f:{(~@:)g/(c;0;(0;a;0;!0;!0);(,b/c:2#-2!b)!,x)};0N!#!f 0;n:1+|/'i-:&/'i:b\!h:f 1;|+|" #"n#@[&*/n;n/i;. h]
a:1 0*\:/:+{. x^"<=>xyz "}'0:$12;f:{(x+y;y+:+/1&-1|x-\:x)}/;(+/*/+/abs@(10 f/)'a;lcm/(#f\)'a)
a:(.'","\*0:$13),&300;n:1+|/2#'b:0N 3#*|(~E@)T/(0;a;0;!0;!0)
 (+//2=r:n#(&*/n){@[x;n/2#y;y 2]}/b
  (3=#:)({[m;r;p]c:100!@/|2#m:{(3>#x 4)&^2 99?100!@/|2#x}T/m;$[2<#v:m 4;m[4]:!0;];$[2<#v;$[0>*v;r:v 2;p[v 2]:*v];]
   $[E m;r;(T@[m;3;,sgn@-/p 4 3];r;p)]}.)/((0;@[a;0;2];0;!0;!0);0;&6))
a:0:$14;(q;C;Q):!'/3#'0 1_+{(."`",*|x;.*-2#x;.'"`",'h 1;.'*h:+0N 2#-3_x:" "\x^",")}'a
 t:-1_*{#*|x}{(x,k;k_@[y;C[k:*&~y]^`;-;1])}//(0#`;@[#'=,/C;`FUEL;0])
 f:{((`FUEL!x){@[x;C y;+;Q[y]*1+(-q y)!-1+x y]}/t)`ORE}
 (f 1;*(-1>-/){@[x;n<f m;m:-2!+/x]}/0,n:*/12#10)

#!../k
abs:{x|-x};sgn:{(x>0)-x<0};gcd:*|(*:)(|!\)/,;lcm:{x*(-gcd[x]y)!y};prm:{(x-1){,/'((,(#*x)#`c$#x),x)(~i=\:i)*i+i<\:i:!1+#x}/+0x00}
D:(0 1;1 0;0 -1;-1 0);I:{T/(0;x;0;y;!0)};E:{99=100!@/x 1 0};Am:{@[x,(0|y+1-#x)#0;y;:;z]}
T:{[i;a;r;u;v]p:(a;::;r+a@)[|3#(5#10)\a i]@'i+1+!3;c:100!a i
 $[c=3;(a;u):(Am[a;*p;*u];1_u);c=4;v,:a@*p;|/c=5 6;i:(i+3;a p 1)2!c+~a@*p;~^j:1 2 7 8?c;a:Am[a;p 2;(+;*;<;=)[j]/a 2#p];c=9;r+:a@*p]
 (i+0|0 4 4 2 2 0 0 4 4 2 c;a;r;u;v)}.
X:{e:x y-1;1@$y;r:z@0:"i/",-2#$100+y;1@$[r~e;," ";`k[(`actual;r;`expected;e)]," "];}@."(",(";"/0:,"o"),")"

X[1;{(+/-2+-3!x;+//1_(0|-2+-3!)\x)}@.:']
X[2;{f:{I[@[x;1 2;:;y];!0].1 0}x;(f 12 2;*&19690720=f'+!2#100)}@.'","\*:]
X[3]{p:+\''a:{0,'(+D"RDLU"?*'x)*\:.'1_'x:","\x}'x;g:|\:/'&':''+:;c:&,/&/~g[p]>r:-g@-p;f:&/^[;0]@[;c]@,/
     (f@+/abs r;f(+\:/+\'abs@+/'a)-+/+/abs(r-*p;r-\:'*|p))}
X[4;{(+/{(|/=':x)>|/<':x:$x}'x;+/{(|/2=#'.=x)>|/<':x:$x}'x)}@{x+!1+y-x}/.'"-"\*:]
X[5]{(*|*|I[.'","\*x]@,)'1 5}
X[6;{(+/{y&1+0^y x}[a]/999*a=a:?/x;#2_,/^/'|:\(!/x)\','$`SAN`YOU)}@|+")"\']
X[7]{a:.'","\*x;p:+prm 5
 (|/*+0{[a;x;y]*|I[a]y,x}[a]/'p
  |/{[a;x]*|*|*|*|({~E z x}.)({z[x;3],:y;z[x;4]:!0;z[x]:{~(#x 4)|E x}T/z x;(5!x+1;*|*|z x;z)}.)/0 0,,@[(0;a;0;:;!0);3;:;]'x}[a]'p+5)}
X[8;{(*/+/'"12"=\:x@*<+/'"0"=x;" #"n#{y^'x}/"01"?x)}(0N;*/n:6 25)#*:]
X[9]{(**|I[(.'","\*x);]@,)'1 2}
X[10]{a:"#"=x;K:1+2*k:-1+|/n:(#a;#*a)
 t:@[;K/2#k;:;-1]@,/(|'|1_'t+2*m),'t:(|+t),1_t+m:|//t:s#(?t@<t)?t:+/>/t*\:'t:!s:2#1+k
 c:#'?'(2##*p)#t@K/k+,/'p-/:'p:n\&,/a
 (-1+|/c;100/|p@'({x^0N}@,/+h@\:!l:|/#'h:{y@<x y}[+/d*d]'h@H@<H:!h:=t@K/k+d:p-p@'*>c)200)}
X[11]{a:.'","\*x;b:10000;g:{[b;p;d;m;h]m[3]:,0^h@b/p;m:{$[1<#*|x;0;~E x]}T/m;$[E m;:h;];v:m 4;h[b/p]:*v;p+:D d:4!d+1 3 v 1;m[4]:!0;(p;d;m;h)}[b].
 f:{[a;b;g;x](`X=@:)g/(c;0;(0;a;0;!0;!0);(,b/c:2#-2!b)!,x)}[a;b;g];r:,#!f 0;n:1+|/'i-:&/'i:b\!h:f 1;r,,|+|" #"n#@[&*/n;n/i;:;h]}
X[12;{f:{(x+y;y+:+/1&-1|x-\:x)}/;(+/*/+/abs@(10 f/)'x;lcm/(#f\)'x)}@1 0*\:/:+{. x^"<=>xyz "}']
X[13]{a:.'","\*x;n:1+|/2#'b:0N 3#*|(~E@)T/(0;a;0;!0;!0)
 (+//2=r:n#(&*/n){[n;x;y]@[x;n/2#y;:;y 2]}[n]/b
  (3=#:)({[m;r;p]c:100!@/|2#m:{(3>#x 4)&^2 99?100!@/|2#x}T/m;$[2<#v:m 4;m[4]:!0;];$[2<#v;$[0>*v;r:v 2;p[v 2]:*v];]
   $[E m;r;(T@[m;3;:;,sgn@-/p 4 3];r;p)]}.)/((0;@[a;0;:;2];0;!0;!0);0;&6))}
X[14]{(q;C;Q):!'/3#'0 1_+{(."`",*|x;.*-2#x;.'"`",'h 1;.'*h:+0N 2#-3_x:" "\x^",")}'x
 t:-1_*(#*|:){[C;x;y](x,k;k_@[y;C[k:*&~y]^`;-;1])}[C]//(0#`;@[#'=,/C;`FUEL;:;0])
 f:{[q;C;Q;t;x]((`FUEL!x){[q;C;Q;x;y]@[x;C y;+;Q[y]*1+(-q y)!-1+x y]}[q;C;Q]/t)`ORE}[q;C;Q;t]
 (f 1;*(-1>-/){[n;f;x]@[x;n<f m;:;m:-2!+/x]}[n;f]/0,n:*/12#10)}
X[15]{
 d::D 3 1 2 0
 m::(0;.'","\*x;0;!0;!0)
 p::u::22 22
 b::.[(-1+2*p)#"?";p;:;"."]
 v::&2
 f::{[d;x]m::{~#x 4}T/@[m;3;,;x+1];r:*m 4;m[4]:!0;$[r=2;v::p+d x;];r}d
 g::{b::.[b;p+:x;:;"."];
     {$[~r:f x;:b::.[b;p+d x;:;"#"];];g d x;f 1 0 3 2 x}'&"?"=b ./:p+/:d;p-:x;}
 g 0 0
 w:{b::b .[;;:;","]/x;?,/{("."=b .)#x+/:d}'x}\,v
 (*&,/~^w?\:,u;-2+#w)}
X[16;{(10/8#100{-48+{[s;n;x]*|$+/(l#-1 1)*-/s@@[;l-1;&;n]'(i+x;i:(-1+x)+(2*x)*!l:1+(-2*x)!n)}[0,+\x;#x]'1+!#x}/x
       10/|-8#100(10!+\)/|(10/7#x)_,/10000#,x)}@.'*:]
X[17]{x:(.'","\*x),&3000;n:(#b),#*b:~"."=-1_"\n"\`c$*|(~|/99 3=100!@/|2#)T/(0;x;0;!0;!0)
 (+/*/n\&,/*/{+y}\{*/'3'0i,x,0i}'+:\b
  *|*|(~E@)T/(0;@[x;0;:;2];0;"A,B,A,C,A,A,C,B,C,B\nL,12,L,8,R,12\nL,10,L,8,L,12,R,12\nR,12,L,8,L,10\nn\n";!0))}
/18
/a:.'","\*0:"i/19";f:{ \**|I[a;]@ \x};(+/f'+!2#50;10000/-99 0+{~f[x]&f x+-99 99}{x+1 0=f x+1 0}/(~f@)(0 1+)/100 0)
X[20]{x:4(+" ",)/x;(m;n):#'(x;*x);x:,/x
 p:*'("."=x@)#'(,/-1 2*/:1,n)+/:l:l^/(1,n)+\:l:&>/"@Z"<\:x
 g:g0:(^"# "?x@)#'(!#x)+(!0;1 -1,n,-n)"."=x
 g[p@|'v],:p v:(2=#:)#.=s:x[l+\:0 1,n]^'" "
 (A;Z):p s?$`AA`ZZ
 f:-1+#{[A;Z;g]{[Z;x](0<#w)&^(w:*|x)?Z}[Z]{[g;x]x,,(,/g@*|x)^x@-2+#x}[g]/,,A}[A;Z]@
 r:f g
 r,f(((#g)>)#'(-1<)#'g:,/g+/:(!30)*#g:@[g0;p@|'v;,;p[v]+((-#x;#x)@|/(c>(m;n)-6)|6>c:(m;n)\p)v])}
X[21]{f:+/(127<)#*|I[.'","\*x;]@
 f'("OR A J\nAND B J\nAND C J\nNOT J J\nAND D J\nWALK\n" /(~(A&B&C))&D
    "OR A J\nAND B J\nAND C J\nNOT J J\nAND D J\nOR I T\nOR F T\nAND E T\nOR H T\nAND T J\nRUN\n")} /..&(H|(E&(I|F)))
X[22]{r:2019{10007!$["c"=*y;x-.*|" "\y;"k"=*|y;-x+1;x*.*|" "\y]}/x /part1
 n:119315717514047;m:101741582076661;rep:{[f;e;x;y]$[y;f/(~2!y)_x,2#o[f;e;x;-2!y];e]};mul:rep[n!+]0;pow:rep[mul]1;inv:pow[;n-2] /part2
 div:{[mul;inv;x;y]mul[x]inv y}[mul]inv
 (b;c):1 0{[n;div;x;y]l:*|" "\y;n!$["c"=*y;x+0,. l;"k"=*|y;-x+!2;x div'. l]}[n;div]/|x
 bm:pow[b]m;r,n!mul[2020;bm]+mul[c]div[bm-1]b-1}
/a:.'","\*0:"i/23";m:{(0;a;0;,x;!0)}'!50;h:N:!0;r:r2:0N /slow
/ f:{(p;m[x;4]):0 3_m[x;4];$[255=*p;N::1_p;m[*p;3],:1_p]}
/ g:{Y:N 1;r::Y^r;$[~^h?Y;r2::Y;];h,:*|m[0;3],:N;N::!0}
/ {^r2}{{$[(~#m[x]3)&3=100!@/|2#m x;m[x;3],:-1;];m[x]:T m x;$[2<#m[x]4;f x;]}'!#m
/       $[(#N)&~+//#''m@\:3 4;g[];]}/0
/ r,r2
X[24]{a:`j$"#"=x;t::&*/25#2;f::{[g;x](1=c)|x<2=c:g[x]+g'x}{(+/'3'0,x,0)-x};r:(1<#:){k:2/,/x;$[t k;:2/|,/x;];t[k]:1;f x}/a /part1
 h:(@[;12;:;!0]@^[;12]'&'1=+/abs i-\:'i:!5 5){@[x;y;,;25 -25+|y]}/((17;!5);(7;20+!5);(11;4+5*!5);(13;5*!5)) /part2
 r,+/*200{(a;h):x;$[|/25#a;a:(&25),a;];$[|/-25#a;a,:&25;];h:((#a)>#:){x,(#x)+25#x}/h;a:(1=c)|a<2=c:+/'0^a h;(a;h)}/(,/a;h)}
\\

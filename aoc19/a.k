abs:{x|-x}
prm:{(x-1){,'/(!1+#x){(,(#*y)#x),y+y>x-1}\:x}/,,0}
T:{[i;a;u;v]c:100!a i;p:(a;::)[|2#(4#10)\a i]@'a i+1 2
 $[c=3;(a[a i+1];u):(*u;1_u);c=4;v,:*p;|/c=5 6;i::(i+3;p 1)2!c+~*p;~^j:1 2 7 8?c;a[a i+3]:(+;*;<;=)[j]/p]
 (i+0|0 4 4 2 2 0 0 4 4 c;a;u;v)}.
I:{T/(0;x;y;!0)}

a:.'0:"01.txt"
+/-2+-3!a
+//1_(0|-2+-3!)\a

a:.'","\*0:"02.txt"
f:**(3>@/)({(@[x;x y+3;(:;+;*)[x y]/x x y+1 2];y+4)}/)/|0,,@[a;1 2;]@
f 12 2
*&19690720=f'+!2#100

a:0:"03.txt"
d:+"RDLU"!4 2#d,-d,:|d:!2
p:+\''b:{0,'(d@\:*'x)*\:.'1_'x:","\x}'a
g:|\:/'&':''+:;c:&,/&/~g[p]>r:-g@-p;f:{&/x[c]^0}
f@,/+/abs r
f@,/(+\:/+\'abs@+/'b)-+/+/abs(r-*p;r-\:'*|p)

a:{x+!1+y-x}/.'"-"\*0:"04.txt"
+/{(|/=':x)>|/<':x:$x}'a
+/{(|/2=#'.=x)>|/<':x:$x}'a

f:{*|*|I[x;,y]}@.'","\*0:"05.txt"
f 1
f 5

b:?/a:|+")"\'0:"06.txt"
+/{x&1+0^x b}/0W-b=b
#2_,/^/'|:\(!/a)\','$`SAN`YOU

a:.'","\*0:"07.txt"
|/*+0{*|I[a;y,x]}/'+prm 5
|/{|/*|*|*{(m;i;s):x;99>100!@/|2#m i}{(m;i;s):x;m[i;2],:s;m[i]:T{&/~99 4=100!@/|2#x}T/m i;(m;5!i+1;*|*|m i)}/({(0;a;x;!0)}'x;0;0)}'+5+prm 5
